using sys { WeakArray, Array, String, log }
using utils { existsInRange }
using string;
using array;
const CR = utf32_(0x0a);

class Node {
    connections = WeakArray(Node);
    isVisited = false;
    isActive = false;

    hasLoop() bool {
        isActive || (!isVisited && {
            isVisited := isActive := true;
            r = connections.contain `n n.hasLoop();
            isActive := false;
            r
        })
    }
}

class Graph {
    nodes = Array(Node);
    fromStr(in String) this {
        byNames = WeakArray(Node).resize('z' + 1);
        getOrCreateNode = \{
            name = in.getCh();
            byNames[name] : {
                r = nodes.append(Node);
                byNames[name] := &r;
                r
            }
        };
        loop {
            from = getOrCreateNode();
            in.getCh();
            from.connections.append(&getOrCreateNode());
            in.getCh() == 0
        }
    }    
    hasLoop() bool {    
        nodes.contain{ _.hasLoop() }
    }
}

log(Graph.fromStr(@"a>b b>c c>d e>f b>e e>a c>c").hasLoop()
    ? "has some loop{CR}"
    : "has no loops{CR}")
