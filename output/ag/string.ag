using sys {
    String,
    Cursor,
    Blob,
    StrBuilder,
    SharedArray
}
using array;

class String {
    *cursor() Cursor { Cursor.set(this) }
    *split(delimiter int) @SharedArray(String) {
        text = Cursor.set(this);
        SharedArray(String).`r{
            loop !(text.getTill(delimiter) ? r.append(_))
        }
    }
}
class Cursor {
    getTill(separator int) ?str {
        peekCh() != 0 ? {
            r = StrBuilder;
            loop !{
                c = getCh();
                c != separator && c != 0 ? r.putCh(c)
            };
            r.toStr()
        }
    }
}

class StrBuilder {
    pos = 0;
    reserve(bytes int) {
        size = capacity();
        pos + bytes >= size ?
            insert(size, size * 2 + 4 + bytes);
    }
    putCh(codePoint int) this {
        reserve(8);
        pos := putChAt(pos, codePoint)
    }
    putStr(s str) this {
        in = Cursor.set(s);
        loop !{
            c = in.getCh();
            c != 0 ? putCh(c)
        }
    }
    putOptStr(s ?str) this { s ? putStr(_) }
    putInt(val int) this {
        reserve(22);
        isNeg = val < 0;
        isNeg ? val *= -1;
        p = pos + 21;
        loop {
            set8At(p -= 1, (val % 10)->{_<0?-_:_} + '0');
            (val /= 10) == 0
        };
        isNeg ? set8At(p -= 1, '-');
        copy(pos, this, p, pos + 21 - p);
        pos += pos + 21 - p;
    }
    newLine() this {
        putCh(0x0a);
    }
    toStr() str {
        mkStr(0, pos).{
            pos := 0
        }
    }
}
