using sys { Blob, log }
using guiPlatform { Canvas, Paint, Rect, Font, Image }
using string;

const w = 1024f;
const h = 768f;

fn forRangeFS(from float, to float, step float, do(float)) {
    from -> loop!(_ += step < to ? do(_));
}

class MyApp {
    ph = 0s;
    x = 100f;
    y = 100f;
    dx = 1f;
    dy = 1f;
    f = Font;
    img = Image;
    +guiPlatform_App {
        onStart() {
            f.fromName("Arial Bold");
            img.fromBlob(Blob.{ _.loadFile("sd.png") : log("Img sd.png not loaded") });
        }
        onPaint(c Canvas) {
            ph += 1s;
            c.clear(0xff_ffffffs);
            p = Paint;
            (x + dx) -> (_ < 0f || _ > w ? dx *= -1f : x := _);
            (y += dy) -> (y < 0f || y > h ? dy *= -1f : y := _);
            c.drawRect(Rect.setXYWH(x-50f, y-50f, 100f, 100f), p.color(0xff_ff0000s));
            forRangeFS(0f, 700f, 4f) {
                p.color(0xff_008800s | (short(_) + ph));
                c.drawLine(_,     0f, 0f,     700f - _, p);
                c.drawLine(w - _, 0f,  w,     700f - _, p);
                c.drawLine(_,      h, 0f, h - 700f + _, p);
                c.drawLine(w - _,  h,  w, h - 700f + _, p);
            };
            c.drawSimpleText((w - 48f) / 2f, (h - 16f) / 2f, "Hello", f, 16f, p.color(0xff_004400s));
            c.drawImage((w - 108f) / 2f, (h - 100f) / 2f - 100f, img);
        }
        onKey(pressed bool, key short, shifts short) {
            sys_log("{}\
                key {int(key)} shifts{int(shifts)}
            ");
            pressed && key == 20s ? sys_setMainObject(?MyApp)
        }
   }
}
MyApp.run("Hello AG", 120)
